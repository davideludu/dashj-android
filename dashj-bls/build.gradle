plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'maven'
    id 'signing'
    id 'kotlin-android'
    id 'kotlin-kapt'
}
android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 1
        versionName "0.18.0"

        externalNativeBuild {
            cmake {
                cFlags '-DHAVE_CONFIG_H -DWORD=32'
                cppFlags ""
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {

        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/java'
    }

    kotlinOptions { jvmTarget = '1.8' }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    externalNativeBuild {
        cmake {
            path 'cpp/CMakeLists.txt'
        }
    }
}

version '0.18.0'

dependencies {
    api 'org.dashj:dashj-bls:0.18.0'
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}


signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'DashJ BLS'
                packaging 'aar'
                groupId 'org.dashj.android'
                artifactId 'dashj-bls-android'
                version '0.18.0'
                description 'DashJ BLS Libary for Android'
                url 'https://github.com/HashEngineering/dashj-bls-android'

                scm {
                    connection 'scm:svn:https://github.com/HashEngineering/dashj-bls-android'
                    developerConnection 'scm:svn:https://github.com/HashEngineering/dashj-bls-android'
                    url 'https://github.com/HashEngineering/dashj-bls-android'
                }

                licenses {
                    license {
                        name 'MIT'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'hashengineering'
                        name 'HashEngineering'
                        email 'hashengineeringsolutions@gmail.com'
                    }
                }
            }
        }
    }
}